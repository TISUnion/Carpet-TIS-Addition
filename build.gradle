plugins {
	id 'maven-publish'
	id 'com.github.hierynomus.license' version '0.16.1' apply false
	id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'fabric-loom' version '1.10-SNAPSHOT' apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version '9d21b334a7'

	// https://github.com/Fallen-Breath/yamlang
	id 'me.fallenbreath.yamlang' version '1.4.1' apply false
}

preprocess {
	strictExtraMappings = false

	def mc114  = createNode('1.14.4', 1_14_04, '')
	def mc115  = createNode('1.15.2', 1_15_02, '')
	def mc116  = createNode('1.16.5', 1_16_05, '')
	def mc117  = createNode('1.17.1', 1_17_01, '')
	def mc118  = createNode('1.18.2', 1_18_02, '')
	def mc119  = createNode('1.19.4', 1_19_04, '')
	def mc1201 = createNode('1.20.1', 1_20_01, '')
	def mc1202 = createNode('1.20.2', 1_20_02, '')
	def mc1204 = createNode('1.20.4', 1_20_04, '')
	def mc1206 = createNode('1.20.6', 1_20_06, '')
	def mc1211 = createNode('1.21.1', 1_21_01, '')
	def mc1213 = createNode('1.21.3', 1_21_03, '')
	def mc1214 = createNode('1.21.4', 1_21_04, '')
	def mc1215 = createNode('1.21.5', 1_21_05, '')

	mc115 .link(mc114 , file('versions/mapping-1.15-1.14.txt'))
	mc115 .link(mc116 , file('versions/mapping-1.15-1.16.txt'))
	mc116 .link(mc117 , file('versions/mapping-1.16-1.17.txt'))
	mc117 .link(mc118 , file('versions/mapping-1.17-1.18.txt'))
	mc118 .link(mc119 , file('versions/mapping-1.18-1.19.txt'))
	mc119 .link(mc1201, file('versions/mapping-1.19-1.20.1.txt'))
	mc1201.link(mc1202, file('versions/mapping-1.20.1-1.20.2.txt'))
	mc1202.link(mc1204, file('versions/mapping-1.20.2-1.20.4.txt'))
	mc1204.link(mc1206, file('versions/mapping-1.20.4-1.20.6.txt'))
	mc1206.link(mc1211, file('versions/mapping-1.20.6-1.21.1.txt'))
	mc1211.link(mc1213, file('versions/mapping-1.21.1-1.21.3.txt'))
	mc1213.link(mc1214, file('versions/mapping-1.21.3-1.21.4.txt'))
	mc1214.link(mc1215, file('versions/mapping-1.21.4-1.21.5.txt'))
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy = DuplicatesStrategy.INCLUDE
			}
		}
	}
}
